pkgname=testssl.sh
pkgver=v3.0.711.ge35f0e8
pkgrel=1
pkgdesc='Testing TLS/SSL encryption'
arch=('any')
url='https://github.com/drwetter/testssl.sh'
license=('GPL2')
depends=('bash' 'coreutils' 'util-linux' 'openssl' 'inetutils' 'bind-tools' 'procps-ng')
source=("$pkgname"::"git+${url}.git#branch=3.1dev")
md5sums=('SKIP')

pkgver() {
    cd $srcdir/$pkgname
    git describe --tags | sed "s/-/./g"
}

package() {
    cd $srcdir/$pkgname

    install -dm755 $pkgdir/usr/lib/testssl/
    cp -r * $pkgdir/usr/lib/testssl/
    
    install -dm755 $pkgdir/usr/bin/
    cat > $pkgdir/usr/bin/testssl << EOF
#!/bin/sh
cd /usr/lib/testssl/
exec /usr/lib/testssl/testssl.sh "\$@"
EOF

    chmod a+x $pkgdir/usr/bin/testssl
}
