pkgname=nikto
pkgver=2.1.6.734.g2aa550f
pkgrel=1
pkgdesc='A web server scanner'
arch=('any')
url='https://github.com/sullo/nikto'
license=('GPL2')
depends=('perl-net-ssleay' 'perl-json')
source=("$pkgname"::"git+${url}.git")
md5sums=('SKIP')
backup=('etc/nikto.conf')

pkgver() {
  cd $pkgname
  git describe --tags | sed "s/-/./g"
}

package() {
  cd $srcdir/$pkgname

  install -dm755 $pkgdir/usr/share/nikto/
  cp -r program/* $pkgdir/usr/share/nikto/

  install -Dm 644 program/nikto.conf.default "${pkgdir}/etc/nikto.conf"
  install -Dm 644 documentation/nikto.1 "${pkgdir}/usr/share/man/man1/nikto.1"
  install -Dm 644 program/docs/nikto_manual.html "${pkgdir}/usr/share/doc/${pkgname}/manual.html"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"

  install -dm755 $pkgdir/usr/bin/
  cat > $pkgdir/usr/bin/nikto << EOF
#!/bin/sh
cd /usr/share/nikto/
exec /usr/bin/perl nikto.pl "\$@"
EOF

  chmod a+x $pkgdir/usr/bin/nikto
}
