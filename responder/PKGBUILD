pkgname=responder
pkgver=v3.0.2.0.12.g24e7b7c
pkgrel=1
pkgdesc='LLMNR/NBT-NS/mDNS Poisoner'
arch=('any')
url='https://github.com/lgandx/Responder'
license=('GPL')
depends=('iproute2' 'python' 'python-cryptography')
source=("$pkgname"::"git+${url}.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed "s/-/./g"
}

package() {
  cd $srcdir/$pkgname

  install -dm755 $pkgdir/usr/lib/responder
  cp -r * $pkgdir/usr/lib/responder

  install -dm755 $pkgdir/usr/bin/

  cat > $pkgdir/usr/bin/responder << EOF
#!/bin/sh
cd /usr/lib/responder
python Responder.py "\$@"
EOF

  cat > $pkgdir/usr/bin/multirelay << EOF
#!/bin/sh
cd /usr/lib/responder/tools
python MultiRelay.py "\$@"
EOF

  cat > $pkgdir/usr/bin/runfinger << EOF
#!/bin/sh
cd /usr/lib/responder/tools
python RunFinger.py "\$@"
EOF

  chmod a+x $pkgdir/usr/bin/responder
  chmod a+x $pkgdir/usr/bin/multirelay
  chmod a+x $pkgdir/usr/bin/runfinger
}
