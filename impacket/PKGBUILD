pkgbase=impacket
pkgname=('impacket' 'python2-impacket')
pkgver=0_9_21.48.g760cb1ea
pkgrel=1
pkgdesc='A collection of Python classes for working with network protocols'
arch=('any')
url='https://github.com/SecureAuthCorp/impacket'
license=('custom')
makedepends=("python-setuptools" "python2-setuptools")
source=("$pkgname"::"git+${url}.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed "s/impacket_//" | sed "s/-/./g"
}

prepare() {
    cp -a impacket{,-py2}
}

build() {
    cd "$srcdir/impacket"
    python setup.py build

    cd "$srcdir/impacket-py2"
    python2 setup.py build
}

package_impacket() {
    depends=('krb5' 'python-crypto' 'python-flask' 'python-future' 'python-ldap3' 'python-ldapdomaindump' 'python-pcapy' 'python-pyasn1' 'python-pycryptodomex' 'python-pyopenssl')
    cd "$srcdir/impacket"
    python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-impacket() {
    depends=('krb5' 'python2-crypto' 'python2-flask' 'python2-future' 'python2-ldap3' 'python2-ldapdomaindump' 'python2-pcapy' 'python2-pyasn1' 'python2-pycryptodomex' 'python2-pyopenssl')
    cd "$srcdir/impacket-py2"
    python2 setup.py install --root="$pkgdir/" --optimize=1

    # We don't keep the files in /usr/bin for the python 2 version
    rm -r "$pkgdir/usr/bin"

    mv "$pkgdir/usr/share/doc/impacket/" "$pkgdir/usr/share/doc/python2-impacket/"
}
