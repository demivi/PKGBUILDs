pkgname=impacket
pkgver=0_9_22.17.gac307704
pkgrel=1
pkgdesc='A collection of Python classes for working with network protocols'
arch=('any')
url='https://github.com/SecureAuthCorp/impacket'
license=('custom')
depends=('krb5' 'python-crypto' 'python-flask' 'python-future' 'python-ldap3' 'python-ldapdomaindump' 'python-pcapy' 'python-pyasn1' 'python-pycryptodomex' 'python-pyopenssl')
makedepends=("python-setuptools")
source=("$pkgname"::"git+${url}.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed "s/impacket_//" | sed "s/-/./g"
}

build() {
    cd "$srcdir/impacket"
    python setup.py build
}

package() {
    cd "$srcdir/impacket"
    python setup.py install --root="$pkgdir/" --optimize=1
}
